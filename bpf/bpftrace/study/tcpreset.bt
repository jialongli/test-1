#!/usr/bin/bpftrace

// 荣涛	2021年6月4日

#include <net/sock.h>
#include <uapi/linux/tcp.h>
#include <uapi/linux/ip.h>

BEGIN
{
	printf("Tracing TCP reset. Hit ctrl-C to end.\n");
	printf("%-8s %-14s %-6s %-14s %-6s\n", "TIME", "LADDR", "LPORT", "RADDR", "RPROT");
}

//static void tcp_v4_send_reset(const struct sock *sk, struct sk_buff *skb)
kprobe:tcp_v4_send_reset
{
	$skb = (struct sk_buff*)arg1;
	$tcp = (struct tcphdr*)($skb->head + $skb->transport_header);
	$ip = (struct iphdr*)($skb->head + $skb->network_header);
	$dport = ($tcp->dest >> 8) | (($tcp->dest << 8) & 0xff00);
	$sport = ($tcp->source >> 8) | (($tcp->source << 8) & 0xff00);

	time("%H:%M:%S ");

	printf("%-14s %-6d %-14s %-6d\n", 
		ntop(AF_INET, $ip->daddr), $dport, ntop(AF_INET, $ip->saddr), $sport);
}